// ========================================
// Adapter-In: REST Admin Servlet
// ========================================
// Inbound adapter for admin REST API (Servlet-based)
// Handles HTTP requests and responses for admin operations
// Middleware: Spring MVC (Servlet)
// NO Lombok allowed
// ========================================

plugins {
    id 'java-library'
    id 'java-test-fixtures'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

// ========================================
// REST Docs Configuration
// ========================================
ext {
    snippetsDir = file('build/generated-snippets')
}

configurations {
    asciidoctorExt
}

dependencies {
    // ========================================
    // Core Dependencies
    // ========================================
    // Application layer (use cases)
    api project(':application')
    api project(':domain')
    api project(':common-auth')

    // Spring Web
    implementation libs.spring.boot.starter.web
    implementation libs.spring.boot.starter.validation

    // Spring Security (Optional)
    implementation libs.spring.boot.starter.security

    // JWT (JJWT)
    implementation libs.jjwt.api
    runtimeOnly libs.jjwt.impl
    runtimeOnly libs.jjwt.jackson

    // JSON Processing
    implementation libs.jackson.databind
    implementation libs.jackson.datatype.jsr310

    // API Documentation (Optional)
    implementation libs.springdoc.openapi

    // ========================================
    // AsciiDoctor Extension
    // ========================================
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor:3.0.3'

    // ========================================
    // Test Dependencies
    // ========================================
    testImplementation libs.spring.boot.starter.test
    testImplementation libs.spring.security.test
    testImplementation libs.rest.assured
    testImplementation libs.spring.restdocs.mockmvc
    testImplementation libs.archunit.junit5
    testImplementation testFixtures(project(":domain"))

    // ========================================
    // Test Fixtures Dependencies
    // ========================================
    // TestFixtures can use main dependencies
    testFixturesApi project(':application')
    testFixturesApi project(':domain')
    testFixturesImplementation libs.jackson.databind
}

// ========================================
// Test Coverage (70% for adapters)
// ========================================
// Note: Foundation 단계(AUT-000)에서는 커버리지 검증 비활성화
// 실제 비즈니스 로직 구현 후 활성화 필요
tasks.jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.00 // TODO: 실제 구현 완료 후 0.70으로 조정
            }
        }
    }
}

tasks.test {
    outputs.dir snippetsDir
    finalizedBy tasks.jacocoTestCoverageVerification
}

// ========================================
// AsciiDoctor → HTML 변환
// ========================================
asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn test

    baseDirFollowsSourceFile()

    attributes(
        'snippets': snippetsDir,
        'source-highlighter': 'highlightjs',
        'toc': 'left',
        'toclevels': 3,
        'sectlinks': true,
        'sectnums': true
    )
}

// ========================================
// AsciiDoctor 결과물을 static/docs로 복사
// ========================================
// 문서 생성 후 수동으로 실행: ./gradlew :adapter-in:rest-api:copyDocs
tasks.register('copyDocs', Copy) {
    dependsOn asciidoctor
    from asciidoctor.outputDir
    into file('src/main/resources/static/docs')
}

// ========================================
// Permission 스캔 (CI/CD용)
// ========================================
// 실행: ./gradlew :adapter-in:rest-api:scanPermissions
// 출력: build/permissions/permissions.json
//
// CI/CD 파이프라인에서 이 파일을 S3/Artifact에 업로드하면
// AuthHub Admin에서 자동완성 소스로 사용 가능
tasks.register('scanPermissions', JavaExec) {
    description = 'Scan @PreAuthorize annotations and output permissions to JSON'
    group = 'verification'

    dependsOn classes

    mainClass = 'com.ryuqq.authhub.adapter.in.rest.architecture.PermissionScanner'
    classpath = sourceSets.test.runtimeClasspath

    args = [
            '--output', "${buildDir}/permissions/permissions.json",
            '--service', 'authhub',
            '--package', 'com.ryuqq.authhub.adapter.in.rest'
    ]

    doFirst {
        mkdir "${buildDir}/permissions"
    }
}
